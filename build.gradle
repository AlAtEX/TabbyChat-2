buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            // for the vanilla plugin
            name = "minecrell"
            url = "http://repo.minecrell.net/snapshots"
        }
    }
    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.2-SNAPSHOT'
    }
}

ext.utils = rootProject.project(':MnM-Utils')
ext.api = rootProject.project(':TabbyChat-API')
ext.forge = rootProject.project(':TabbyChat-Forge')
ext.common = rootProject.project(':TabbyChat-Common')
ext.rev = '0.21'

apply plugin: 'forge'
apply from: utils.file('gradle/minecraft.gradle')

group = 'mnm.mods'
archivesBaseName = 'TabbyChat'
version = '2.0.0-beta2a'
def bnum = System.env.BUILD_ID
if (bnum != null) {
  version += "-SNAPSHOT-$bnum"
}

dependencies {
    // for the runClient
    compile utils
}
jar {
    manifest {
        attributes "ModType":"FML,LiteLoader"
        attributes "ModSide":"CLIENT"
    }
}
task jarDev(type:Jar) {
    classifier "dev"
}
artifacts.archives jarDev
afterEvaluate {
    evaluationDependsOnChildren()
    // add content to jars
    [jar, jarDev]*.with {
       from([api, common, forge].collect { it.sourceSets.main.output })
    }
}
// disable things
tasks.findAll{task -> task.name.contains('eclipse')}*.enabled = false
subprojects {
    repositories {
        flatDir  {
            dirs rootProject.file('libs')
        }
    }
    afterEvaluate {
        if (it in [api, common, forge]) {
            reobf.enabled = false
        }
        // disable runs in subprojects
        tasks.findAll {it.name.startsWith 'run'}*.enabled = false
}}

allprojects.findAll {!(it in [project, api, forge, common])}.each { project ->
    project.afterEvaluate {
        project.tasks.withType(Jar) { jar ->
            if (jar.name == 'repackMinecraft') return; // exclude minecraft

            def pname = project.name.capitalize()
            def jname = jar.name.capitalize()
            def copy = task "copy$pname$jname"(type: Copy) {
                dependsOn project.build
                from jar.archivePath
                into rootProject.jar.destinationDir
                outputs.upToDateWhen {false}
            }
            rootProject.build.dependsOn copy
        }
    }
}
