buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
	}
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'org.spongepowered.mixin'


ext.gitRevision = cmd('git rev-list --count HEAD')
ext.gitHash = cmd('git rev-parse --short HEAD')

version = "2.3.0-SNAPSHOT-$gitRevision"

archivesBaseName = 'tabbychat'
group = 'mnm.mods'

minecraft {
	version = '1.12'
	mappings = 'snapshot_20170608'
	runDir = 'run'
	// Replace the version string
	replace(
		"@VERSION@": project.version,
		"@REVISION@": project.gitRevision,
		"@MCVERSION@": project.minecraft.version
	)
}
sourceSets {
	main {
		refMap = 'mixin.tabbychat.refmap.json'
	}
}
dependencies {

	compile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'

	testCompile 'junit:junit:4.12'

}
litemod.json {
	name = 'tabbychat'
	author = 'killjoy1221'
	revision = ext.gitRevision
	displayName = 'TabbyChat 2'
	description = 'Chat overhaul.'
	mixinConfigs = ['mixin.tabbychat.json']
//	metadata {
//		url = 'https://minecraft.curseforge.com/projects/tabbychat-2'
//		updateUrl = 'https://raw.githubusercontent.com/killjoy1221/Version/master/TabbyChat2.json'
//	}
}
jar {
	from litemod
	manifest {
		attributes 'Implementation-Version': "$version (git $gitHash)"
	}
}
task srgJar(type: Jar) {
	classifier 'srg'
	baseName = "mod-${archivesBaseName.toLowerCase()}"
	manifest {
		attributes 'Implementation-Version': "$version (git $gitHash)"
	}
}

reobf {
	srgJar {
		mappingType = 'SEARGE'
	}
}
mixin {
	defaultObfuscationEnv notch
}

def cmd(s) {
	try {
		s.execute(null, file('.')).text.trim()
	} catch(e) {
		'null'
	}
}