plugins{
	id 'net.minecraftforge.gradle.tweaker-client' version '2.0.2'
}
ext {
	utils = rootProject.project(':MnM-Utils')
	api = rootProject.project(':api')
	forge = rootProject.project(':forge')
	macros = rootProject.project(':macros')

	rev = '0.31'

}
def buildServer = System.env.BUILD_ID != null

group = 'mnm.mods'
archivesBaseName = 'TabbyChat'
version = "2.0-beta-SNAPSHOT.${System.env.BUILD_ID}"
if (buildServer) version += ".${System.env.GIT_COMMIT?.substring(0,6)}"
// version = '2.0.0-beta3.1'

apply from: utils.file('gradle/minecraft.gradle')

minecraft{
	// Replace the version string
	replace(
		"@VERSION@": project.version,
		"@REVISION@": project.rev,
		"@MCVERSION@": project.minecraft.version
	)
	tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
}

allprojects {
	repositories {
		flatDir  {
			dirs rootProject.file('flat')
		}
	}
}
dependencies {
	// Add util library
	deobfProvided 'com.mumfrey:liteloader:1.8-SNAPSHOT:srg'

	provided utils

	compile api
	compile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'

	testCompile 'junit:junit:4.12'
}

jar {
	extension 'litemod'
	from(configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}) {
		exclude 'META-INF/***'
	}
	from {forge.sourceSets.main.output}
}
sourceJar {
	from {[api,forge]*.sourceSets.main.allSource}
}
task jarSrg(type:Jar) {
	dependsOn jar
	classifier "srg"
	from zipTree(jar.archivePath)
}
artifacts.archives jarSrg

reobf {
	jar.task.dependsOn jarSrg
	jarSrg {
		useSrgSrg()
	}
}

afterEvaluate {
	evaluationDependsOnChildren()
	if (buildServer) {
		// disable extra artifacts on build server
		allprojects {
			tasks.withType(Jar).findAll {
				it.classifier in ['srg', 'sources']
			}*.enabled = false
		}
		// also disable reobf tasks
		[utils.reobf.jarSrg,reobf.jarSrg]*.task*.enabled = false
		// find jars and move their destination dir to /build/libs of root project
		[utils,macros].each {it.jar.destinationDir file('build/libs')}
	}
}
