buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
	}
	dependencies.classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
}
plugins {
	id 'com.github.johnrengelman.shadow' version '1.2.3'
}
ext {
	utils = rootProject.project(':MnM-Utils')
	api = rootProject.project(':api')
	forge = rootProject.project(':forge')
	compat = rootProject.project(':compat')

	rev = '17'

}
apply plugin: 'net.minecraftforge.gradle.liteloader'
version = '2.0.2-SNAPSHOT'
// version = '2.0.2'

apply from: utils.file('gradle/minecraft.gradle')
version += "-b$gitRevision"

group = 'mnm.mods'
archivesBaseName = 'TabbyChat'

minecraft {
	// Replace the version string
	replace(
		"@VERSION@": project.version,
		"@REVISION@": project.gitRevision,
		"@MCVERSION@": project.minecraft.version
	)
}

dependencies {
	// Add util library
	shadow utils

	shadow api
	shadow 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'

	testCompile 'junit:junit:4.12'
}
jar {
	classifier "srg"
	appendix "mc$minecraft.version"

	from api.sourceSets.main.output
	from {forge.sourceSets.main.output}
	from {compat.sourceSets.main.output}
}
shadowJar {
	from {compat.sourceSets.main.output}
	extension 'litemod'
	appendix "mc$minecraft.version"
}
sourceJar {
	from {[api,forge,compat]*.sourceSets.main.allSource}
	appendix "mc$minecraft.version"
}

reobf {
	shadowJar {}
	jar {
		mappingType = 'SEARGE'
	}
}

afterEvaluate {
//	evaluationDependsOnChildren()
	if (System.env.BUILD_ID != null && !System.env.BUILD_ID.isEmpty()) {
		// disable extra artifacts on build server
		allprojects {
			tasks.withType(Jar).findAll {
				it.classifier in ['srg', 'sources']
			}*.enabled = false
		}
		// also disable reobf tasks
		[utils.reobf.jarSrg,reobf.jarSrg]*.task*.enabled = false
		// find jars and move their destination dir to /build/libs of root project
		utils.jar.destinationDir file('build/libs')
	}
}
