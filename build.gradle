buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.tweaker-client'

ext.utils = rootProject.project(':MnM-Utils')
ext.api = rootProject.project(':TabbyChat-API')
ext.forge = rootProject.project(':TabbyChat-Forge')
ext.common = rootProject.project(':TabbyChat-Common')
ext.rev = '0.31'

group = 'mnm.mods'
archivesBaseName = 'TabbyChat'
version = '2.0.0-beta3.1'
def bnum = System.env.BUILD_ID
if (bnum != null) {
  version += "-SNAPSHOT-$bnum"
}
apply from: utils.file('gradle/minecraft.gradle')

dependencies {
    // for the reobf
    compile utils
}
jar {
    manifest {
        attributes "ModType":"FML,LiteLoader"
        attributes "ModSide":"CLIENT"
    }
}
task jarSrg(type:Jar) {
    classifier "srg"
}
artifacts.archives jarSrg
afterEvaluate {
    evaluationDependsOnChildren()
    // add content to jars
    [jar, jarSrg]*.with {
       from([api, common, forge].collect { it.sourceSets.main.output })
    }
}

subprojects {
    repositories {
        flatDir  {
            dirs rootProject.file('libs')
        }
    }
    afterEvaluate {
        //if (it in [api, common, forge]) {
        //    reobfJar.enabled = false
        //}
        // disable runs in subprojects
        tasks.findAll {it.name.startsWith 'run'}*.enabled = false
		makeStart.enabled = false
	}
}

allprojects.findAll {
	!(it in [project, api, forge, common])
}.each { project ->
	project.afterEvaluate {
		project.tasks.withType(Jar) { jar ->
			if (jar.name == 'repackMinecraft') return
			destinationDir = rootProject.jar.destinationDir
		}
	}
}
