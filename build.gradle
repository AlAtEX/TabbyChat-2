buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            // for the vanilla plugin
            name = "minecrell"
            url = "http://repo.minecrell.net/snapshots"
        }
    }
    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.1-SNAPSHOT'
    }
}
allprojects {
    apply plugin: 'java' // make sure all the projects have their tasks
    version = "$versionMajor.$versionMinor.$versionRevision-beta2-SNAPSHOT"
    group = "mnm.mods"
}

ext.utils = rootProject.project(':MnM-Utils')
ext.api = rootProject.project(':TabbyChat-API')
ext.forge = rootProject.project(':TabbyChat-Forge')
ext.common = rootProject.project(':TabbyChat-Common')

apply plugin: 'net.minecrell.vanilla.base'
apply from: utils.file('gradle/minecraft.gradle')

archivesBaseName = "TabbyChat"

// troublesome. SpecialSource thinks drawCenteredString belongs to ChatTab
minecraft.srgExtra "MD: mnm/mods/tabbychat/gui/ChatTab/drawCenteredString (Lnet/minecraft/client/gui/FontRenderer;Ljava/lang/String;III)V bub/a (Lbty;Ljava/lang/String;III)V"

jar {
    from api.sourceSets.main.output
    from common.sourceSets.main.output
    from forge.sourceSets.main.output

    manifest {
        attributes "ModType":"FML,LiteLoader"
        attributes "ModSide":"CLIENT"
    }
}
task jarDev(type:Jar) {
    from api.sourceSets.main.output
    from common.sourceSets.main.output
    from forge.sourceSets.main.output

    classifier "dev"
}
artifacts.archives jarDev
// disable eclipse
tasks.findAll{task -> task.name.contains('eclipse')}.each {task -> task.enabled = false}
