buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            // for the vanilla plugin
            name = "minecrell"
            url = "http://repo.minecrell.net/snapshots"
        }
    }
    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.2-SNAPSHOT'
    }
}
ext.utils = rootProject.project(':MnM-Utils')
ext.api = rootProject.project(':TabbyChat-API')
ext.forge = rootProject.project(':TabbyChat-Forge')
ext.common = rootProject.project(':TabbyChat-Common')

apply plugin: 'forge'
version = "$versionMajor.$versionMinor.$versionRevision-beta2a"
apply from: utils.file('gradle/minecraft.gradle')

group = "mnm.mods"

evaluationDependsOnChildren()

archivesBaseName = "TabbyChat"

dependencies {
    // for the runClient
    compile utils
}
jar {
    from api.sourceSets.main.output
    from common.sourceSets.main.output
    from forge.sourceSets.main.output

    manifest {
        attributes "ModType":"FML,LiteLoader"
        attributes "ModSide":"CLIENT"
    }
}
task jarDev(type:Jar) {
    from api.sourceSets.main.output
    from common.sourceSets.main.output
    from forge.sourceSets.main.output

    classifier "dev"
}
artifacts.archives jarDev

// copy MnmUtils into the libs folder
task copyUtils(type:Copy) {
    from utils.jar.archivePath
    into jar.destinationDir
}
task copyUtilsDev(type:Copy) {
    from utils.jarDev.archivePath
    into jar.destinationDir
}
build.dependsOn copyUtils, copyUtilsDev

// disable things
tasks.findAll{task -> task.name.contains('eclipse')}.each {task -> task.enabled = false}
subprojects {
    repositories {
        flatDir  {
            dirs rootProject.file('libs')
        }
    }
    if (it != utils) {
	reobf.enabled = false
    }
    // disable runs in subprojects
    tasks.findAll {it.name.startsWith 'run'}.each {it.enabled = false}
}
