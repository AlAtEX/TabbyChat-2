buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://file.minecraftforge.net/maven'
		}
	}
	dependencies.classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
}
apply plugin: 'net.minecraftforge.gradle.tweaker-client'

ext {
	utils = rootProject.project(':MnM-Utils')
	api = rootProject.project(':api')
	forge = rootProject.project(':forge')
	macros = rootProject.project(':macros')

	rev = '0.31'

}
def buildServer = System.env.BUILD_ID != null

group = 'mnm.mods'
archivesBaseName = 'TabbyChat'
version = "2.0-mc${minecraft.version}-beta-SNAPSHOT.${System.env.BUILD_ID}"
if (buildServer) version += ".${System.env.GIT_COMMIT?.substring(0,6)}"
// version = '2.0.0-beta3.1'

apply from: utils.file('gradle/minecraft.gradle')

minecraft{
	// Replace the version string
	replace(
		"@VERSION@": project.version,
		"@REVISION@": project.rev,
		"@MCVERSION@": project.minecraft.version
	)
	tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
}

allprojects {
	repositories {
		flatDir  {
			dirs rootProject.file('flat')
		}
	}
}
dependencies {
	// Add util library
	deobfProvided 'com.mumfrey:liteloader:1.8-SNAPSHOT:srgnames'

	provided utils

	compile api
	compile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'

	testCompile 'junit:junit:4.12'
}
sourceJar {
	from {[api,forge]*.sourceSets.main.allSource}
}
task jarSrg(type:Jar) {
	classifier "srg"

	from sourceSets.main.output}
	from api.sourceSets.main.output
	from forge.sourceSets.main.output
}
task releaseJar(type:Jar) {
	extension 'litemod'

	from sourceSets.main.output
	from api.sourceSets.main.output
	from forge.sourceSets.main.output

	from(configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}) {
		exclude 'META-INF/***'
	}
}
artifacts.archives jarSrg, releaseJar

reobf {
	releaseJar {}
	jarSrg {
		useSrgSrg()
	}
}

afterEvaluate {
	evaluationDependsOnChildren()
	if (buildServer) {
		// disable extra artifacts on build server
		allprojects {
			tasks.withType(Jar).findAll {
				it.classifier in ['srg', 'sources']
			}*.enabled = false
		}
		// also disable reobf tasks
		[utils.reobf.jarSrg,reobf.jarSrg]*.task*.enabled = false
		// find jars and move their destination dir to /build/libs of root project
		[utils,macros].each {it.jar.destinationDir file('build/libs')}
	}
}
